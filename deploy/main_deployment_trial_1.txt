terraform {
  backend "remote" {
    organization = "coconut_projects"
    workspaces {
      name = "urhobo_dictionary_workspace"
    }
  }

  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 5.39"
    }
  }
}

provider "aws" {
  region = "ca-central-1"
}

# -- VPC --
data "aws_availability_zones" "available" { state = "available" }

locals {
  azs_count = 2
  azs_names = data.aws_availability_zones.available.names

}

resource "aws_vpc" "main" {
  cidr_block           = "10.30.0.0/16"
  enable_dns_hostnames = true
  enable_dns_support   = true
  tags                 = {
    Name = "urhobo_dictionary_vpc"
  }
}

resource "aws_subnet" "public" {
  count                   = local.azs_count
  vpc_id                  = aws_vpc.main.id
  availability_zone       = local.azs_names[count.index]
  cidr_block              = cidrsubnet(aws_vpc.main.cidr_block, 8, 10 + count.index)
  map_public_ip_on_launch = true
  tags                    = {
    Name = "urhobo_dictionary_vpc_subnet_${local.azs_names[count.index]}"
  }
}

# -- Internet Gateway --
resource "aws_internet_gateway" "main" {
  vpc_id = aws_vpc.main.id
  tags   = {
    Name = "urhobo_dictionary_igw"
  }
}

resource "aws_eip" "main" {
  count      = local.azs_count
  depends_on = [aws_internet_gateway.main]
  tags       = { Name = "urhobo_dictionary_eip_${local.azs_names[count.index]}" }
}

resource "aws_route_table" "public" {
  vpc_id = aws_vpc.main.id
  tags   = { Name = "urhobo_dictionary_rt_public" }

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.main.id
  }
}

resource "aws_route_table_association" "public" {
  route_table_id = aws_route_table.public.id
  subnet_id      = aws_subnet.public[count.index].id
  count          = local.azs_count
}

resource "aws_ecs_cluster" "main" {
  name = "urhobo-dictionary-cluster"
}

# -- ECS NODE ROLE --
# A policy document that allows a Service to assume roles
data "aws_iam_policy_document" "ecs_node_doc" {
  statement {
    actions = ["sts:AssumeRole"]
    effect  = "Allow"

    principals {
      identifiers = ["ec2.amazonaws.com"]
      type        = "Service"
    }
  }
}

# An IAM role for the ECS Service
resource "aws_iam_role" "ecs_node_role" {
  name_prefix = "urhobo-dictionary-ecs-node-role-"

  # Allow only ECS services to assume this role
  assume_role_policy = data.aws_iam_policy_document.ecs_node_doc.json

  inline_policy {
    name = "log-stream-creation-policy"

    policy = jsonencode({
      Version   = "2012-10-17",
      Statement = {
        Action = [
          "logs:CreateLogStream",
          "logs:PutLogEvents",
          "logs:DescribeLogStreams"
        ]
        Effect = "Allow"

        Resource = "arn:aws:logs:*:*:*"
      }
    })
  }
}

# Attach the IAM policy that allows the ECS Services control EC2 resources
resource "aws_iam_role_policy_attachment" "ecs_node_role_policy" {
  policy_arn = "arn:aws:iam::aws:policy/service-role/AmazonEC2ContainerServiceforEC2Role"
  role       = aws_iam_role.ecs_node_role.name
}

# An instance profile used to pass the role information to the individual EC2 instances
resource "aws_iam_instance_profile" "ecs_node" {
  name_prefix = "urhobo-dictionary-ecs-node-profile-"
  path        = "/ecs/instance/"
  role        = aws_iam_role.ecs_node_role.name
}

# -- ECS TASK ROLE --
data "aws_iam_policy_document" "ecs_task_doc" {
  statement {
    actions = ["sts:AssumeRole"]
    effect  = "Allow"

    principals {
      identifiers = ["ecs-tasks.amazonaws.com"]
      type        = "Service"
    }
  }
}

resource "aws_iam_role" "ecs_task_role" {
  assume_role_policy = data.aws_iam_policy_document.ecs_task_doc.json

  name_prefix = "urhobo-dictionary-ecs-task-role-"

  inline_policy {
    name = "log-stream-creation-policy"

    policy = jsonencode({
      Version   = "2012-10-17",
      Statement = {
        Action = [
          "logs:CreateLogStream",
          "logs:PutLogEvents",
          "logs:DescribeLogStreams"
        ]
        Effect = "Allow"

        Resource = "arn:aws:logs:*:*:*"
      }
    })
  }

  inline_policy {
    name = "allow-read-environment-file"

    policy = jsonencode({
      Version   = "2012-10-17",
      Statement = [
        {
          Action = [
            "s3:GetObject"
          ]

          Effect = "Allow"

          Resource = "arn:aws:s3:::personalappenvfiles/urhobo_dictionary_server.env"
        },
        {
          Effect = "Allow"
          Action = [
            "s3:GetBucketLocation"
          ]
          Resource = [
            "arn:aws:s3:::personalappenvfiles/",
            "arn:aws:s3:::personalappenvfiles",
            "arn:aws:s3:::personalappenvfiles/*"
          ]
        }
      ]
    })
  }
}

resource "aws_iam_role" "ecs_exec_role" {
  assume_role_policy = data.aws_iam_policy_document.ecs_task_doc.json
  name_prefix        = "urhobo-dictionary-ecs-exec-role-"

  inline_policy {
    name = "log-stream-creation-policy"

    policy = jsonencode({
      Version   = "2012-10-17",
      Statement = {
        Action = [
          "logs:CreateLogStream",
          "logs:PutLogEvents",
          "logs:DescribeLogStreams"
        ]
        Effect = "Allow"

        Resource = "arn:aws:logs:*:*:*"
      }
    })
  }

  inline_policy {
    name = "allow-read-environment-file"

    policy = jsonencode({
      Version   = "2012-10-17",
      Statement = [
        {
          Action = [
            "s3:GetObject"
          ]

          Effect = "Allow"

          Resource = "arn:aws:s3:::personalappenvfiles/urhobo_dictionary_server.env"
        },
        {
          Effect = "Allow"
          Action = [
            "s3:GetBucketLocation"
          ]
          Resource = [
            "arn:aws:s3:::personalappenvfiles/",
            "arn:aws:s3:::personalappenvfiles",
            "arn:aws:s3:::personalappenvfiles/*"
          ]
        }
      ]
    })
  }
}

resource "aws_iam_role_policy_attachment" "ecs_exec_role_policy" {
  policy_arn = "arn:aws:iam::aws:policy/service-role/AmazonECSTaskExecutionRolePolicy"
  role       = aws_iam_role.ecs_exec_role.name
}

# -- SECURITY GROUPS --
resource "aws_security_group" "ecs_security_group" {
  name_prefix = "urhobo-dictionary-ecs-security-group-"
  vpc_id      = aws_vpc.main.id

  ingress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = [aws_vpc.main.cidr_block, "0.0.0.0/0"]
  }

  # Allow out-going TCP traffic to any address from ports 0 - 65535
  egress {
    from_port   = 0
    to_port     = 65535
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }
}

data "aws_ssm_parameter" "ecs_node_ami" {
  name = "/aws/service/ecs/optimized-ami/amazon-linux-2023/recommended/image_id"
}

resource "aws_launch_template" "ecs_ec2" {
  name_prefix            = "urhobo-dictionary-ec2-"
  image_id               = data.aws_ssm_parameter.ecs_node_ami.value
  instance_type          = "t2.micro"
  vpc_security_group_ids = [aws_security_group.ecs_security_group.id]
  iam_instance_profile {
    arn = aws_iam_instance_profile.ecs_node.arn
  }

  monitoring { enabled = true }
  user_data = base64encode(<<-EOF
#!/bin/bash
echo ECS_CLUSTER=${aws_ecs_cluster.main.name} >> /etc/ecs/ecs.config;
  EOF
  )
}

resource "aws_autoscaling_group" "ecs" {
  max_size                  = 2
  min_size                  = 1
  name_prefix               = "urhobo-dictionary-ecs-asg-"
  vpc_zone_identifier       = aws_subnet.public[*].id
  health_check_grace_period = 1500 // In seconds
  health_check_type         = "EC2"
  protect_from_scale_in     = false

  launch_template {
    id      = aws_launch_template.ecs_ec2.id
    version = "$Latest"
  }

  tag {
    key                 = "Name"
    propagate_at_launch = true
    value               = "urhobo_dictionary_cluster"
  }

  tag {
    key                 = "AmazonECSManaged"
    propagate_at_launch = true
    value               = ""
  }
}

resource "aws_ecs_capacity_provider" "main" {
  name = "urhobo-dictionary-ecs-capacity-provider"
  auto_scaling_group_provider {
    auto_scaling_group_arn         = aws_autoscaling_group.ecs.arn
    managed_termination_protection = "DISABLED"

    managed_scaling {
      maximum_scaling_step_size = 2
      minimum_scaling_step_size = 1
      status                    = "ENABLED"
      target_capacity           = 100
    }
  }
}

resource "aws_ecs_cluster_capacity_providers" "main" {
  cluster_name       = aws_ecs_cluster.main.name
  capacity_providers = [aws_ecs_capacity_provider.main.name]
  default_capacity_provider_strategy {
    capacity_provider = aws_ecs_capacity_provider.main.name
    base              = 1
    weight            = 100
  }
}

# -- AWS ECR --
resource "aws_ecr_repository" "main" {
  name                 = "urhobo-dictionary-server"
  image_tag_mutability = "MUTABLE"
  force_delete         = true
  image_scanning_configuration {
    scan_on_push = true
  }
}

output "urhobo_dictionary_repo_url" {
  value = aws_ecr_repository.main.repository_url
}

# -- CLOUD WATCH --
resource "aws_cloudwatch_log_group" "ecs" {
  name              = "/ecs/urhobo-dictionary"
  retention_in_days = 14
}

resource "aws_cloudwatch_log_stream" "ecs" {
  log_group_name = aws_cloudwatch_log_group.ecs.name
  name           = "urhobo-dictionary-server-task-stream"
  depends_on     = [aws_cloudwatch_log_group.ecs]
}

# -- ECS TASK DEFINITION --

#resource "aws_efs_file_system" "fs" {
#  creation_token = "urhobo-dictionary-server-file-system"
#}

resource "aws_ecs_task_definition" "main" {
  family             = "urhobo-dictionary"
  task_role_arn      = aws_iam_role.ecs_task_role.arn
  execution_role_arn = aws_iam_role.ecs_exec_role.arn
  network_mode       = "bridge"
  cpu                = 900
  memory             = 900

  #  volume {
  #    name = "gradle-cache"
  #    efs_volume_configuration {
  #      file_system_id = aws_efs_file_system.fs.id
  #      root_directory = "~/.gradle/"
  #    }
  #  }

  container_definitions = jsonencode([
    {
      name         = "urhobo-dictionary-server"
      image        = "${aws_ecr_repository.main.repository_url}:latest"
      essential    = true
      portMappings = [{ containerPort = 8080, hostPort = 80 }]

      environmentFiles = [
        {
          value = "arn:aws:s3:::personalappenvfiles/urhobo_dictionary_server.env"
          type  = "s3"
        }
      ]
      cpu               = 800
      memoryReservation = 800

      logConfiguration = {
        logDriver = "awslogs"
        options   = {
          "awslogs-region" : "ca-central-1"
          "awslogs-group" : aws_cloudwatch_log_group.ecs.name
          "awslogs-stream-prefix" : "urhobo-dictionary-server"
        }
      }
    }
  ])
}

# -- ECS TASK SECURITY GROUP --
resource "aws_security_group" "ecs_task_security_group" {
  name_prefix = "ecs-task-sg-"
  description = "Allow all traffic within the VPC"
  vpc_id      = aws_vpc.main.id

  ingress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = [aws_vpc.main.cidr_block]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}

resource "aws_ecs_service" "main" {
  name            = "urhobo-dictionary-server"
  cluster         = aws_ecs_cluster.main.id
  task_definition = aws_ecs_task_definition.main.arn
  desired_count   = 1

  health_check_grace_period_seconds = 1500

  force_new_deployment = true

  triggers = {
    redeployment = plantimestamp()
  }

  #  network_configuration {
  #    security_groups = [aws_security_group.ecs_task_security_group.id]
  #    subnets         = aws_subnet.public[*].id
  #  }

  capacity_provider_strategy {
    capacity_provider = aws_ecs_capacity_provider.main.name
    base              = 1
    weight            = 100
  }

  # Make sure that each service instance is spread out as equally as possible across availability zones
  ordered_placement_strategy {
    type  = "spread"
    field = "attribute:ecs.availability-zone"
  }

  lifecycle {
    ignore_changes = [desired_count]
  }

  depends_on = [aws_lb_target_group.ecs_urhobo_dictionary_target_group]
  load_balancer {
    container_name   = "urhobo-dictionary-server"
    container_port   = 8080
    target_group_arn = aws_lb_target_group.ecs_urhobo_dictionary_target_group.arn
  }
}

# -- APPLICATION LOAD BALANCER --
resource "aws_security_group" "ecs_load_balancer" {
  name_prefix = "urhobo-dictionary-lb-sg-"
  description = "Allow all HTTP/HTTPS traffic from public"
  vpc_id      = aws_vpc.main.id

  dynamic "ingress" {
    for_each = [80, 443]
    content {
      protocol    = "tcp"
      from_port   = ingress.value
      to_port     = ingress.value
      cidr_blocks = ["0.0.0.0/0"]
    }
  }

  egress {
    protocol    = "-1"
    from_port   = 0
    to_port     = 0
    cidr_blocks = ["0.0.0.0/0"]
  }
}

resource "aws_lb" "ecs_load_balancer" {
  name               = "urhobo-dictionary-lb"
  load_balancer_type = "application"
  subnets            = aws_subnet.public[*].id
  security_groups    = [aws_security_group.ecs_load_balancer.id]
}

resource "aws_lb_target_group" "ecs_urhobo_dictionary_target_group" {
  name   = "urhobo-dictionary-lb-tg"
  vpc_id = aws_vpc.main.id

  protocol = "HTTP"
  port     = 80
  #  target_type = "ip"

  health_check {
    enabled             = true
    path                = "/health-check"
    port                = 80
    matcher             = 200
    interval            = 300
    timeout             = 120
    healthy_threshold   = 2
    unhealthy_threshold = 3
  }
}

resource "aws_lb_listener" "ecs_urhobo_lb_listener" {
  load_balancer_arn = aws_lb.ecs_load_balancer.arn
  port              = 80
  protocol          = "HTTP"

  default_action {
    type             = "forward"
    target_group_arn = aws_lb_target_group.ecs_urhobo_dictionary_target_group.arn
  }
}

output "alb_url" {
  value = aws_lb.ecs_load_balancer.dns_name
}

# -- AWS DB Instance --
variable "db_username" {
  default = "urhobo-dictionary"
}

variable "db_password" {
  default = ""
}

resource "aws_security_group" "db_main" {
  vpc_id = aws_vpc.main.id

  ingress {
    from_port   = 5432
    to_port     = 5432
    protocol    = "tcp"
    cidr_blocks = [aws_vpc.main.cidr_block]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}

resource "aws_db_subnet_group" "main" {
  subnet_ids  = aws_subnet.public[*].id
  name_prefix = "urhobo-dictionary-db-subnet-"
}

resource "aws_db_instance" "main" {
  instance_class          = "db.t3.micro"
  allocated_storage       = 10
  db_name                 = "urhobodictionarydb"
  engine                  = "postgres"
  username                = var.db_username
  password                = var.db_password
  identifier              = "urhobo-dictionary-db"
  backup_retention_period = 7
  vpc_security_group_ids  = [aws_security_group.db_main.id]
  multi_az                = false // Single-AZ to qualify for free tier
  db_subnet_group_name    = aws_db_subnet_group.main.name
  #  final_snapshot_identifier = "urhobo-dictionary-db-final-snapshot"
  skip_final_snapshot     = true
}

output "db_url" {
  value = aws_db_instance.main.endpoint
}

